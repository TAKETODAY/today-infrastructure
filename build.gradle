plugins {
  id 'io.freefair.aspectj' version '8.0.1' apply false
  id 'org.unbroken-dome.xjc' version '2.0.0' apply false
  id 'com.github.ben-manes.versions' version '0.46.0'
  id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
  id 'de.undercouch.download' version '5.4.0'
//  id 'me.champeau.mrjar' version '0.1.1' // TODO java 21
}

ext {
  moduleProjects = subprojects.findAll { it.name.startsWith("today-") } + [
          project(":infra-build:infra-gradle-plugin"),
          project(":infra-build:infra-maven-plugin"),
          project(":infra-build:infra-app-loader"),
          project(":infra-build:infra-app-loader-tools"),
          project(":infra-build:infra-jarmode-layertools")
  ]
  javaProjects = subprojects - [
          project(":infra-build:infra-code-coverage"),
          project(":infra-bom"), project(":infra-build"), project(":infra-platform")]
}

configure(allprojects) { project ->
  repositories {
    mavenLocal()

//    maven { url "https://maven.aliyun.com/repository/public" }
//		maven { url "https://repo.huaweicloud.com/repository/maven" }

    mavenCentral()
    maven {
      url "https://repo.spring.io/milestone"
      content {
        // Netty 5 optional support
        includeGroup 'io.projectreactor.netty'
      }
    }
    if (version.contains('-')) {
      maven { url "https://repo.spring.io/milestone" }
    }
    if (version.endsWith('-SNAPSHOT')) {
      maven { url "https://repo.spring.io/snapshot" }
    }
  }
  configurations.configureEach {
    resolutionStrategy {
      cacheChangingModulesFor 0, "seconds"
      cacheDynamicVersionsFor 0, "seconds"
    }
  }
}

configure([rootProject] + javaProjects) { project ->
  group = "cn.taketoday"

  apply plugin: "java"
  apply plugin: 'jacoco'
  apply plugin: "java-test-fixtures"
  apply plugin: 'cn.taketoday.build.conventions'
  apply from: "${rootDir}/gradle/toolchains.gradle"

  configurations {
    dependencyManagement {
      canBeConsumed = false
      canBeResolved = false
      visible = false
    }
    matching { it.name.endsWith("Classpath") }.configureEach {
      it.extendsFrom(dependencyManagement)
    }
  }

  jacocoTestReport {
    enabled = false
  }

  dependencies {
    dependencyManagement(enforcedPlatform(dependencies.project(path: ":infra-platform")))
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testImplementation("org.junit.jupiter:junit-jupiter-params")
    testImplementation("org.junit.platform:junit-platform-suite-api")
    testImplementation("org.mockito:mockito-core")
    testImplementation("org.mockito:mockito-junit-jupiter")
    testImplementation("io.mockk:mockk")
    testImplementation("junit:junit")
    testImplementation("org.assertj:assertj-core")
    testImplementation 'org.projectlombok:lombok'
    testAnnotationProcessor("org.projectlombok:lombok:1.18.22")

    // Pull in the latest JUnit 5 Launcher API to ensure proper support in IDEs.
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    testRuntimeOnly("org.junit.platform:junit-platform-suite-engine")
    // JSR-305 only used for non-required meta-annotations
    compileOnly("com.google.code.findbugs:jsr305")
    testCompileOnly("com.google.code.findbugs:jsr305")
  }

  ext.javadocLinks = [
          "https://docs.oracle.com/en/java/javase/17/docs/api/",
          "https://jakarta.ee/specifications/platform/9/apidocs/",
          "https://docs.jboss.org/hibernate/orm/5.6/javadocs/",
          "https://www.eclipse.org/aspectj/doc/released/aspectj5rt-api/",
          "https://www.javadoc.io/doc/com.fasterxml.jackson.core/jackson-core/2.14.1/",
          "https://www.javadoc.io/doc/com.fasterxml.jackson.core/jackson-databind/2.14.1/",
          "https://www.javadoc.io/doc/com.fasterxml.jackson.dataformat/jackson-dataformat-xml/2.14.1/",
          "https://hc.apache.org/httpcomponents-client-5.2.x/current/httpclient5/apidocs/",

  ] as String[]
}

configure(moduleProjects) { project ->
  apply from: "${rootDir}/gradle/infra-module.gradle"
}

configure(rootProject) {
  description = "TODAY Infrastructure"
  apply plugin: 'cn.taketoday.build.api-diff'
}
