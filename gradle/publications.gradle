apply plugin: "signing"
apply plugin: "maven-publish"

boolean isSnapshot = version.endsWith('SNAPSHOT')
String repoUsername = System.getProperty("repoUsername")
String repoPassword = System.getProperty("repoPassword")

publishing.publications.withType(MavenPublication.class) {
  pom {
    afterEvaluate {
      name = project.name
      description = project.description
    }
    url = "https://github.com/TAKETODAY/today-infrastructure"
    organization {
      name = "TODAY Tech"
      url = "https://taketoday.cn"
    }
    licenses {
      license {
        name = "GNU GENERAL PUBLIC LICENSE Version 3, 29 June 2007"
        url = "https://www.gnu.org/licenses/gpl.txt"
        distribution = "repo"
      }
    }
    scm {
      url = "https://github.com/TAKETODAY/today-infrastructure.git"
      connection = "scm:git:git://github.com/TAKETODAY/today-infrastructure"
      developerConnection = "scm:git:git://github.com/TAKETODAY/today-infrastructure"
    }
    developers {
      developer {
        id = "taketoday"
        name = "Harry Yang"
        email = "taketoday@foxmail.com"
      }
    }
    issueManagement {
      system = "GitHub"
      url = "https://github.com/TAKETODAY/today-infrastructure/issues"
    }
  }
  versionMapping {
    usage('java-api') {
      fromResolutionResult()
    }
    usage('java-runtime') {
      fromResolutionResult()
    }
  }
}

if (!isSnapshot) {
  signing {
    sign publishing.publications
  }
}

// Disable Gradle module metadata publishing until we know what we want.
// https://docs.gradle.org/8.3/userguide/publishing_gradle_module_metadata.html#sub:disabling-gmm-publication
tasks.withType(GenerateModuleMetadata).configureEach {
  enabled = false
}

publishing {
  repositories {
    maven {
      url = isSnapshot
          ? 'https://oss.sonatype.org/content/repositories/snapshots/'
          : 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'

      credentials {
        username = repoUsername
        password = repoPassword
      }
    }
  }
}
