group = "Documentation"

javadoc {
  description = "Generates aggregated Javadoc API documentation."
  title = "${rootProject.description} ${version} API"

  options.encoding = "UTF-8"
  options.memberLevel = JavadocMemberLevel.PROTECTED
  options.header = rootProject.description
  options.overview = "docs/api/overview.html"

  options.links("https://docs.oracle.com/en/java/javase/17/docs/api/")
  options.links("https://jakarta.ee/specifications/platform/9/apidocs/")
  options.links("https://fasterxml.github.io/jackson-core/javadoc/2.10/")

  source(rootProject.sourceSets.main.allJava)
  maxMemory = "1024m"
  destinationDir = file("$buildDir/docs/javadoc")

}

asciidoctorj {
  def docRoot = 'https://docs.taketoday.cn'
  def docsFramework = "${docRoot}/today-framework/docs/${rootProject.version}" as Object
  version = '2.4.1'
  fatalWarnings ".*"
  options doctype: 'book', eruby: 'erubis'
  attributes([
          icons                      : 'font',
          idprefix                   : '',
          idseparator                : '-',
          docinfo                    : 'shared,private-header', // https://docs.asciidoctor.org/asciidoctor/latest/docinfo/
          revnumber                  : rootProject.version,
          sectanchors                : '',
          sectnums                   : '',
          'source-highlighter'       : 'highlight.js',
          highlightjsdir             : 'js/highlight',
          'highlightjs-theme'        : 'github',
          stylesdir                  : 'css/',
          stylesheet                 : 'stylesheet.css',
          'today-version'            : rootProject.version,
          'today-framework-main-code': 'https://github.com/TAKETODAY/today-framework/tree/main',
          'doc-root'                 : docRoot,
          'docs-today-framework'     : docsFramework,
          'api-today-framework'      : "${docsFramework}/javadoc-api/cn/taketoday"
  ])
}

/**
 * Reference documentation from "src/docs build/docs/html5".
 */
asciidoctor {
  sourceDir file('docs')
  sources {
    include '*.adoc'
  }
  outputDir "$buildDir/docs/html5"
  logDocuments = true
  resources {
    from(sourceDir) {
      include 'images/*.png', 'css/**', 'js/**'
    }
    // projectDir + '/docs/theme/js
    from("$projectDir/docs/theme/css") {
      into "css"
    }
  }
}

/**
 * Reference documentation from "src/docs in "build/docs/pdf".
 */
asciidoctorPdf {
  sourceDir("doc")
  sources {
    include '*.adoc'
  }
  outputDir "$buildDir/docs/pdf"
  logDocuments = true
}

