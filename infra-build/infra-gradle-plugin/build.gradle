/*
 * Copyright 2017 - 2024 the original author or authors.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program. If not, see [https://www.gnu.org/licenses/]
 */

plugins {
  id "java-gradle-plugin"
//  id "com.gradle.plugin-publish" version "1.2.1"
}

description = "TODAY Infrastructure Applications Gradle Plugins"

configurations.configureEach {
  resolutionStrategy {
    eachDependency { dependency ->
      // Downgrade Jackson as Gradle cannot cope with 2.15.0's multi-version
      // jar files with bytecode in META-INF/versions/19
      if (dependency.requested.group.startsWith("com.fasterxml.jackson")) {
        dependency.useVersion("2.14.2")
      }
    }
  }
}

dependencies {

  api(project(":today-core"))
  api("com.fasterxml.jackson.module:jackson-module-parameter-names")
  api("com.fasterxml.jackson.core:jackson-databind")
  api("org.apache.httpcomponents.client5:httpclient5")
  api("org.apache.commons:commons-compress")
  api("net.java.dev.jna:jna-platform")
  api("org.tomlj:tomlj:1.0.0")
  implementation(project(":today-aop"))

  implementation(project(":today-core"))
  implementation(project(":today-test-support"))
  implementation(project(":infra-build:infra-app-loader"))
  implementation(project(":infra-build:infra-app-loader-tools"))
  implementation 'org.apache.commons:commons-compress'
  implementation 'io.spring.gradle:dependency-management-plugin:1.1.5'

  optional "org.graalvm.buildtools:native-gradle-plugin:$nativeBuildToolsVersion"

  testImplementation(project(":today-aop"))
  testImplementation(project(":today-core-aot"))
  testImplementation(project(":today-framework"))
  testImplementation("com.tngtech.archunit:archunit-junit5:0.22.0")
  testImplementation("org.assertj:assertj-core")
  testImplementation("org.junit.jupiter:junit-jupiter")
  testImplementation("org.mockito:mockito-core")
  testImplementation("org.testcontainers:junit-jupiter")
  testImplementation("org.testcontainers:testcontainers")
  testImplementation 'org.jboss.logging:jboss-logging:3.5.3.Final'
  testImplementation(project(":today-test-support"))
  testImplementation("com.jayway.jsonpath:json-path")
  testImplementation("org.testcontainers:testcontainers")
  testImplementation("org.hamcrest:hamcrest")
  testImplementation("org.mockito:mockito-junit-jupiter")
  testImplementation("org.skyscreamer:jsonassert")
  testImplementation "commons-io:commons-io"
}

gradlePlugin {
  automatedPublishing = true
  website = "https://github.com/TAKETODAY/today-infrastructure"
  vcsUrl = "https://github.com/TAKETODAY/today-infrastructure"
  plugins {
    infraPlugin {
      id = "infra.application"
      tags.set(['web', 'application'])
      displayName = "TODAY Infrastructure Applications Gradle Plugin"
      description = "TODAY Infrastructure Applications Gradle Plugin"
      implementationClass = "infra.gradle.plugin.InfraApplicationPlugin"
    }
    infraAotPlugin {
      id = "infra.application.aot"
      tags.set(['web', 'application', 'aot'])
      displayName = "TODAY Infrastructure Applications AOT Gradle Plugin"
      description = "TODAY Infrastructure Applications AOT Gradle Plugin"
      implementationClass = "infra.gradle.plugin.InfraApplicationAotPlugin"
    }
  }
}

tasks.named('test') {
  inputs.dir('src/docs/gradle').withPathSensitivity(PathSensitivity.RELATIVE).withPropertyName('buildScripts')
}

