

plugins {
	id 'java'
	id 'cn.taketoday.application' version '{version}'
}

subprojects {
	apply plugin: 'java'
	group = 'org.example.projects'
	version = '1.2.3'
	if (it.name == 'bravo') {
		dependencies {
			implementation(project(':charlie'))
		}
	}
}

infraJar {
	mainClass = 'com.example.Application'
	layered {
		application {
			intoLayer("static") {
				include "META-INF/resources/**", "resources/**", "static/**", "public/**"
			}
			intoLayer("app")
		}
		dependencies {
			intoLayer("snapshot-dependencies") {
				include "*:*:*SNAPSHOT"
				excludeProjectDependencies()
			}
			intoLayer("subproject-dependencies") {
				includeProjectDependencies()
			}
			intoLayer("commons-dependencies") {
				include "org.apache.commons:*" 
			}
			intoLayer("dependencies")
		}
		layerOrder = ["dependencies", "commons-dependencies", "snapshot-dependencies", "subproject-dependencies", "static", "app"]
	}
}

repositories {
	mavenCentral()
	maven { url "file:repository" }
}

dependencies {
	implementation(project(':alpha'))
	implementation(project(':bravo'))
	implementation("com.example:library:1.0-SNAPSHOT")
	implementation("org.apache.commons:commons-lang3:3.9")
	implementation("cn.taketoday:today-core:4.0.0-Draft.3")
}

task listLayers(type: JavaExec) {
	classpath = infraJar.outputs.files
	systemProperties = [ "jarmode": "layertools" ]
	args "list"
}

task extractLayers(type: JavaExec) {
	classpath = infraJar.outputs.files
	systemProperties = [ "jarmode": "layertools" ]
	args "extract"
}
