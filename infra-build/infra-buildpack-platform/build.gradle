plugins {
  id "java-library"
}

description = "Infra App Buildpack Platform"

configurations.configureEach {
  resolutionStrategy {
    eachDependency { dependency ->
      // Downgrade Jackson as Gradle cannot cope with 2.15.0's multi-version
      // jar files with bytecode in META-INF/versions/19
      if (dependency.requested.group.startsWith("com.fasterxml.jackson")) {
        dependency.useVersion("2.14.2")
      }
    }
  }
}

dependencies {
  api(project(":today-core"))
  api("com.fasterxml.jackson.module:jackson-module-parameter-names")
  api("com.fasterxml.jackson.core:jackson-databind")
  api("org.apache.httpcomponents.client5:httpclient5")
  api("org.apache.commons:commons-compress")
  api("net.java.dev.jna:jna-platform:5.7.0")
  api("org.tomlj:tomlj:1.0.0")

  testImplementation(project(":today-test-support"))
  testImplementation("com.jayway.jsonpath:json-path")
  testImplementation("org.testcontainers:testcontainers")
  testImplementation("org.hamcrest:hamcrest")
  testImplementation("org.mockito:mockito-junit-jupiter")
  testImplementation("org.skyscreamer:jsonassert")
}
